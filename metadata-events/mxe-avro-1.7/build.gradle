configurations {
  avsc
}

apply plugin: 'com.commercehub.gradle.plugin.avro'
apply plugin: 'java'

dependencies {
  compile externalDependency.avro_1_7
  compile externalDependency.avroCompiler_1_7
  constraints {
    implementation('commons-collections:commons-collections:3.2.2') {
      because 'Vulnerability Issue'
    }
  }

  avsc project(':metadata-events:mxe-schemas')

  constraints {
    implementation("org.apache.logging.log4j:log4j-core:2.15.0") {
      because("previous versions are vulnerable to CVE-2021-44228")
    }
    implementation("org.apache.logging.log4j:log4j-api:2.15.0") {
      because("previous versions are vulnerable to CVE-2021-44228")
    }
  }
}

def genDir = file("src/generated/java")

task avroCodeGen(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask, dependsOn: configurations.avsc) {
  source("$rootDir/metadata-events/mxe-schemas/src/renamed/avro")
  outputDir = genDir
  dependsOn(':metadata-events:mxe-schemas:renameNamespace')
}

compileJava.source(avroCodeGen.outputs)

idea {
  module {
    sourceDirs += genDir
    generatedSourceDirs += genDir
  }
}

project.rootProject.tasks.idea.dependsOn(avroCodeGen)

// Exclude classes from avro-schemas
jar {
  dependsOn classes
  from sourceSets.main.output
  exclude('com/linkedin/events/**')
}